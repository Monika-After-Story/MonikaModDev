label sayori_exclusive_1:
    scene bg club_day
    with wipeleft_scene
    if not renpy.music.get_playing(channel='music') == audio.t2:
        play music t2 fadeout 1
    "Man..."
    "It looks like no one wants to be bothered today."
    "I slump down into the nearest desk."
    "How am I supposed to occupy myself with something literature-related by myself like this?"
    "I guess I could always read some of the book Yuri gave me..."
    "...But I'm feeling a little too tired to read."
    "I could probably fall asleep right now."
    "I close my eyes and end up listening in on Sayori's conversation with Monika."
    show sayori 1g at t21 zorder 2
    s "We're probably gonna seem really lame compared to all the other clubs, though..."
    show monika 2r at f22 zorder 3
    m "Hmm..."
    m 2b "Well, we can't give up."
    m "The festival is our chance to show everyone what literature is all about!"
    m 2d "The problem is that the idea of a literature club sounds too dense and intellectual..."
    m "But it's not like that at all, you know?"
    m 2a "We just need a way of showing that to everyone..."
    m "Something that speaks to their creative minds."
    show monika at t22 zorder 2
    show sayori at f21 zorder 3
    s 1i "Mmmmmmmmmmm....."
    s "That doesn't solve the problem, though!"
    show sayori at t21 zorder 2
    show monika at f22 zorder 3
    m 2d "Eh? What do you mean?"
    show monika at t22 zorder 2
    show sayori at f21 zorder 3
    s 1c "Even if we come up with the most fun thing ever..."
    s "Nobody will come in the first place if it's a literature event."
    s "So it's more important to figure out how to get people to show up in the first place, you know?"
    s 1a "And after they come, we can do the thing to speak to their creative minds."
    "...What's this?"
    "Sayori is taking this really seriously."
    "It's rare to hear her deliberating like this."
    show sayori at t21 zorder 2
    show monika at f22 zorder 3
    m 2a "Huh, that's a good point..."
    m "In that case, do you think food will do the trick?"
    show monika at t22 zorder 2
    show sayori at f21 zorder 3
    s 1n "W-What kind?!"
    show sayori at t21 zorder 2
    show monika at f22 zorder 3
    m 2b "Ah... Well, I guess we could--"
    show monika at t22 zorder 2
    show sayori 4r at f21 zorder 3
    s "Cupcakes!"
    show sayori at t21 zorder 2
    show monika 1k at f22 zorder 3
    m "Ahaha. Good thinking."
    m 1a "Natsuki would love to do that."
    show monika at t22 zorder 2
    show sayori at f21 zorder 3
    s 4x "Ah! You're right!"
    s "Natsuki makes the best cupcakes!"
    s "That works out perfectly~"
    show sayori at t21 zorder 2
    show monika at f22 zorder 3
    m 1d "That wasn't why you suggested it...?"
    show monika at t22 zorder 2
    show sayori at f21 zorder 3
    s 4q "Cupcakes speak to my creative tummy~"
    show sayori at t21 zorder 2
    show monika at f22 zorder 3
    m 1l "..."
    m "Cupcakes it is, then."
    show monika at t22 zorder 2
    show sayori at f21 zorder 3
    s 1g "I'm hungry..."
    show sayori at t21 zorder 2
    show monika at f22 zorder 3
    m 1a "Anyway, we still need to work out the details of the event itself..."
    scene black
    with close_eyes
    "I find myself smiling."
    "In the end, Sayori is still her usual self."
    "But therein lies the unexpected reason I admire her."
    "Unlike me, who has trouble finding any motivation at all..."
    "Sayori can put her mind to things and make them come to life."
    "I suppose that's why I end up letting her get on my case about things."
    "I can't help but wonder what it would be like to see the world through her eyes..."
    scene bg club_day
    show sayori 1b at face
    with open_eyes
    mc "Uwaa--!"
    "I open my eyes to find Sayori's face filling my vision."
    "I nearly fall out of my chair."
    show sayori at t11 zorder 2
    s 4q "Ehehe, sorry~"
    s 4i "Wait!"
    s 1j "Actually, I'm not sorry at all!"
    s "It's your fault for sleeping like that!"
    s "This isn't the napping club!"
    mc "Does our school have a napping club...?"
    s 1h "You're staying up late again, aren't you?"
    s "Now that you're in a club, you're gonna have less time for anime, you know!"
    s "You'll need to get used to it!"
    mc "Don't say that so loud...!"
    "I glance over my shoulder to see if Monika overheard."
    s 1g "It's true, though..."
    mc "Yeah..."
    mc "I know, I know."
    mc "You're always looking out for me, Sayori."
    s 4q "Ehehe~"
    s "It's what I do best!"
    mc "...That's a problem!"
    mc "What about you?"
    mc "You look out for me better than you look out for yourself."
    mc "You're still oversleeping every day, aren't you?"
    s 1l "Eh?"
    s "N-Not every day!"
    mc "That's not very convincing..."
    mc "How many days this past week have you gotten up on time?"
    s 1m "That's..."
    s 1o "...It's a secret!"
    mc "I knew it..."
    s 5a "C'monnnn!"
    s "At least give me the benefit of the doubt..."
    mc "I can't even do that."
    mc "Look, Sayori, it's written all over you."
    s 1b "Eh...?"
    "Sayori glances around at herself."
    s "How is it written all over me?"
    mc "You were clearly in a rush this morning..."
    mc "Look, your hair is sticking out all around here."
    show sayori at h11
    s 1e "Ah--"
    "I run my fingertips down the side of Sayori's hair, trying to straighten it out."
    mc "Man, you really need a brush for this..."
    s 1y "My hair is just really hard to get right..."
    mc "I won't fall for that."
    mc "There's more than just your hair."
    mc "Look, your bow isn't straight, either."
    mc "And there's a toothpaste stain on your collar right here."
    "I try to wipe off the stain with my finger."
    show sayori at s11
    s "B-But nobody would ever notice that..."
    mc "Of course they would."
    mc "Nobody's gonna tell you about it because they don't want to embarrass you."
    mc "Fortunately, I really don't care about that."
    s 5c "Hey, you meanie..."
    mc "And you don't even keep your blazer buttoned up."
    mc "Seriously, Sayori..."
    mc "Why do you think you don't have a boyfriend yet?"
    show sayori 1h at t11 zorder 2
    s "Eh??"
    s "That's {i}super{/i} mean!"
    mc "Sorry, but you'll thank me later..."
    "I start to button her blazer from the bottom."
    mc "Once you see how much better it looks, you'll change your mind."
    $ persistent.clear[6] = True
    scene s_cg1
    with dissolve_cg
    s "Ehehe~"
    s "This is so funny."
    mc "What is?"
    s "Well..."
    s "I was just thinking how weird it is to have a friend who does these kinds of things."
    mc "Eh?"
    mc "D-Don't say that!"
    mc "You'll make {i}me{/i} feel weird about it, stupid..."
    s "It's okay, though."
    s "I'm happy we're like this."
    s "Aren't you?"
    mc "Ah--"
    mc "I-I guess..."
    s "Hey, be careful..."
    s "The button might come off!"
    mc "Why is this one so hard to close...?"
    "I struggle to fully close the button near her chest."
    mc "Does this thing even fit you properly?"
    s "Ehehe~"
    s "It did when I bought it."
    mc "Sigh..."
    mc "If you ever buttoned it, you would have noticed sooner that it doesn't fit you anymore."
    mc "What are you smiling about?"
    s "It means my boobs got bigger again!"
    mc "D-Don't say that out loud!!"
    s "Ehehe~"
    mc "Anyway..."
    mc "You look much better now, so..."
    mc "Ah..."
    "...Why does it feel strange to see Sayori's blazer buttoned up like that?"
    s "But it's so stuffy..."
    s "Uuu..."
    s "It's not worth it at all!"
    "Sayori hastily unbuttons her blazer once more."
    scene bg club_day
    show sayori 1q at i11 zorder 2
    with dissolve_cg
    s "Phew!"
    s "That's so much better~"
    "Sayori puts her arms out and twirls around."
    s 1a "So if I keep it unbuttoned then I won't get a boyfriend, right?"
    mc "What kind of logic is that?"
    mc "And why are you saying that like it's a good thing?"
    s "Because..."
    s 4h "...If I had a boyfriend, then he wouldn't even let you do things like this!"
    s 4y "And you take care of me better than anyone else would, anyway..."
    s "So that's why I'm keeping it unbuttoned!"
    mc "Stop saying all these embarrassing things!"
    s 1b "Eh?"
    s "I didn't say anything embarrassing..."
    mc "Jeez..."
    mc "Well anyway, just focus on trying to wake up a little earlier..."
    s 1j "Only if you focus on going to bed earlier!"
    mc "Fine, fine..."
    mc "It's a deal."
    s 4q "Ehehe~"
    s 4x "I guess we really are better at taking care of each other than we are at taking care of ourselves."
    mc "Yeah, I guess so, huh..."
    s 4y "So maybe you should come wake me up in the morning!"
    mc "You're doing it again, Sayori..."
    s 1h "Aw, but I was joking that time!"
    mc "Man, it's impossible to tell with you sometimes."
    show monika 3b at l31 behind sayori
    m "Okay, everyone!"
    mc "Eh?"
    "Monika suddenly calls out."
    m "Why don't we share the poems we wrote now?"
    show sayori 4r at h11
    s "Yay~!"
    s 4x "[player], I can't wait to read yours!"
    mc "Yeah, same..."
    "I fail to sound enthusiastic, but Sayori still trots away to retrieve her poem."
    show sayori at thide behind monika
    return

label sayori_exclusive_2:
    if not renpy.music.get_playing(channel='music') == audio.t2:
        stop music fadeout 1.0
    scene bg club_day
    with wipeleft_scene
    if not renpy.music.get_playing(channel='music') == audio.t2:
        play music t2
    s "[player], [player]!"
    "Sayori suddenly comes up to me."
    show sayori 1x at t11 zorder 2
    s "I'm gonna go get some supplies from another classroom."
    s "Want to come with me?"
    mc "Supplies?"
    mc "What for?"
    s 2a "Well, you know how the festival is coming up?"
    s "Me and Monika were gonna make some posters and stuff."
    s "So I need to go find some crayons, and markers, and glue sticks..."
    mc "Ah, I see."
    mc "Sure, I'll go with you."
    s 4q "Yaay~"
    s 4x "Okay Monika, we'll be back soon!"
    show sayori at t22 zorder 2
    show monika 1a at f21 zorder 3
    m "Ah, are you going with [player] to get the supplies?"
    m "There's no need to trouble yourself."
    m "I'd be happy to go with him."
    show monika at t21 zorder 2
    show sayori 1h at s22
    s "Aw, but I wanted to go!"
    s "It's so much fun exploring empty classrooms and stuff!"
    show monika at f21 zorder 3
    m 2j "Hehe, okay, okay."
    m 2a "It was just a suggestion."
    m "See if you can find poster paper too, okay?"
    show monika at t21 zorder 2
    show sayori at f22 zorder 3
    s 1r "Okaay~!"
    s 1a "Ready, [player]?"
    mc "Yep, let's go."
    scene bg corridor
    show sayori 4a at t11 zorder 2
    with wipeleft_scene
    "Sayori and I exit the clubroom."
    "I follow behind as Sayori hums and skips around the hallway."
    "Honestly..."
    "It feels like I'm taking a kid to the mall or something."
    "Sayori finds pleasure in the simplest things sometimes."
    mc "Hey, Sayori..."
    mc "What exactly are we doing for the festival, anyway?"
    mc "I'm not sure how you would make an event out of literature..."
    s 1q "Ehehe!"
    s 1x "Me and Monika have it all planned out!"
    s "Don't you worry~"
    mc "Is that so...?"
    s "Yup!"
    s "We're gonna do a poetry performance!"
    mc "A performance...?"
    mc "Of what kind?"
    s 1c "Well..."
    s "Everyone is gonna take turns on stage..."
    s 1x "And recite their favorite poems!"
    mc "Ah..."
    mc "That sounds..."
    "...Kind of dull?"
    s 1h "[player]!"
    s "You're not thinking about it the right way at all!"
    s "It's not just about reading poems..."
    s 1x "It's about performing them!"
    s "Like, you say the lines of the poem like..."
    s 2j "{i}Between my feet...{/i}"
    s "{i}The last remaining flower beckons to me.{/i}"
    s 1c "{i}I twist the stem, freeing it from its clinging roots...{/i}"
    s "{i}Caressing the final joyous moment between my fingers.{/i}"
    s 3g "{i}But to what ends have I summoned this joy?{/i}"
    s "{i}For now when I look in every direction...{/i}"
    s 1j "{i}The once-prosperous field before me...{/i}"
    show sayori at h11
    s 4m "{i}Is but a barren wasteland!{/i}"
    s "..."
    s 1r "Like that!!"
    mc "Sayori..."
    "How do I put this..."
    mc "I'm sure it's just me, but it's impossible for me to take you seriously when you talk like that..."
    show sayori 5c at s11
    s "Ehhh?"
    s "You meanie!"
    s "I'm working super hard on this, you know!"
    mc "Ah, I know, I know!"
    mc "I just meant that it's a pretty unordinary contrast to your cute self."
    show sayori 4s at t11 zorder 2
    s "Ahaha! Don't say that, it's embarrassing!"
    s 4y "But I guess that means I'm doing a good job~"
    mc "Yeah, I guess so..."
    show sayori at h11
    s 4r "Aaah, I'm so excited!"
    s "The festival is going to be so much fun~"
    "Sayori spins herself around in the hallway again."
    s 1x "Hey, [player], this classroom over here is empty!"
    s "Let's begin the mission!"
    show sayori at thide zorder 1
    hide sayori
    mc "The mission, eh...?"
    "It's been a long time since I've spent time with Sayori like this."
    "But in the end, she hasn't changed one bit."
    "She's nothing but a ball of sunshine, drawing happy vibes from the world around her."
    "It's a pretty nostalgic feeling for me."
    "As the years went by, I began to hole myself up in my room more and more."
    "So going adventuring with Sayori brings about a special sort of feeling I forgot I had in me."
    scene bg class_day
    with wipeleft_scene
    "The two of us enter the classroom."
    "Sayori heads straight to the closet, and I follow."
    show sayori 1b at t11 zorder 2
    s "Let's see what we have in here..."
    s 4x "...Crayons!!"
    "Sayori pulls a box full of crayons off the shelf."
    s "They're the best brand, too!"
    s 1b "They're kind of dirty, though..."
    "Sayori starts pulling various crayons out of the box, reading the color names."
    mc "Alright, that's one down."
    mc "Don't get distracted, we still need to find--"
    s 1a "Wait, I'm looking for my favorite color..."
    mc "Fine, fine..."
    mc "Then at least move aside so I can look for the poster paper."
    s 1b "Ah, I dropped one by accident--"
    play sound "sfx/smack.ogg"
    show white zorder 4:
        alpha 0.6
        linear 0.25 alpha 0.0
    show sayori 2p at h11
    "{i}Smack!{/i}"
    hide white
    s "Kya--!"
    "Sayori bends over and smacks her forehead right into the shelf."
    "She falls to the floor, and the crayons spill all over her lap."
    show sayori 4p at s11
    s "Owowowowow..."
    mc "You okay?"
    s "My forehead..."
    "Sayori clutches her forehead."
    mc "Jeez, Sayori..."
    mc "That's just like you, isn't it?"
    mc "C'mon, let me see."
    "Since Sayori is sitting on the floor, I grab her by the waist and pull her out of the closet."
    mc "You have to move your hands, Sayori..."
    s 4g "But it hurts..."
    mc "Just do it for a second."
    $ persistent.clear[7] = True
    scene s_cg2_base1
    show s_cg2_exp2
    with dissolve_cg
    "Sayori slowly releases her hands from her forehead."
    "I gently brush her bangs to the side."
    show s_cg2_exp1 at cgfade
    show s_cg2_exp3 at cgfade
    s "Ow--!"
    mc "Sorry..."
    "There's a huge red mark on the center of her forehead."
    "A bump is starting to form, as well."
    mc "Man, that's gonna swell up."
    mc "I should find you some ice..."
    hide s_cg2_exp3
    hide s_cg2_exp1
    s "[player]..."
    mc "Where would I even find ice around this time...?"
    mc "Ah, I guess a cold drink would do..."
    s "You don't have to--!"
    show s_cg2_exp2 at cgfade
    hide s_cg2_exp2
    s "I'm fine with--looking like a unicorn--"
    "Even wincing from the pain, Sayori makes a silly joke."
    mc "Ahaha, what are you saying?"
    mc "I'll be right back, okay?"
    s "O-Okay..."
    stop music fadeout 1.0
    scene bg corridor with wipeleft_scene
    "I pat Sayori on the shoulder and run out into the hallway."
    "I locate the nearest vending machine."
    mc "What should I get...?"
    "It doesn't really matter, since it will be used as an ice pack, rather than to drink."
    "But I know Sayori likes apple juice, so I purchase that one."
    "In just a moment, I'm already returning to the classroom where I left Sayori."
    scene s_cg2_base1
    show s_cg2_exp2
    with wipeleft_scene
    play music t9
    "She has one palm on her forehead and is using the other hand to clumsily scoop crayons back into the box."
    s "At least they were already in the wrong spots before I spilled them..."
    mc "Sayori, here."
    show s_cg2_base2 at cgfade behind s_cg2_exp2
    "I hand Sayori the bottle of apple juice."
    show s_cg2_exp2 at cgfade
    hide s_cg2_exp2
    s "It's nice and cold..."
    "Sayori opens the cap and starts drinking from it."
    mc "Sayori, what are you doing?!"
    mc "It's for your forehead, idiot!"
    show s_cg2_exp3 at cgfade
    s "Ah--"
    s "Sorry, I forgot~"
    s "Ahahaha!"
    mc "How hard did you hit your head...?"
    "Sayori places the bottle against the bump on her head."
    show s_cg2_exp1 at cgfade
    s "It stings..."
    mc "Just bear with it, it'll feel better soon."
    mc "Looks like you cleaned up most of the crayons, so that's good."
    hide s_cg2_exp1
    hide s_cg2_exp3
    s "Hey, [player]..."
    s "This kind of reminds you of growing up, doesn't it...?"
    mc "Eh? What do you mean?"
    s "You know how we used to play outside all the time..."
    s "I would always try to keep up with you."
    s "You were kind of oblivious in some ways..."
    s "Like I usually fell behind or had trouble climbing on the things you did..."
    s "But sometimes when I tried to do things I couldn't, I would get myself hurt."
    s "I'd fall and scrape myself, or get a bump..."
    s "And I would start crying really hard."
    show s_cg2_exp3 at cgfade
    s "Ahaha!"
    s "And you would rush over as quick as you could."
    hide s_cg2_exp3
    s "You would try really hard to get me to stop crying."
    s "It was almost like you blamed yourself and were afraid of getting in trouble if someone found out..."
    s "Even though it really wasn't your fault at all, you know?"
    mc "Did I really do that...?"
    s "Yeah...you don't remember?"
    mc "Come to think of it, maybe I do remember a bit..."
    mc "I guess I was always so focused on my games that I didn't pay enough attention to you."
    mc "So in a way, it was my fault."
    mc "Kind of like this time, too..."
    mc "If I wasn't rushing you out of the closet, you probably wouldn't have hit your head."
    s "[player]..."
    s "I don't think you realize it, but you're always thinking about other people."
    s "Even after all these years..."
    s "You're rushing to help me, even though I'm just being clumsy."
    show s_cg2_exp3 at cgfade
    s "You're really a sweetheart..."
    mc "D-Don't call me that!"
    mc "And I don't really do this kind of thing all the time..."
    mc "I guess when it comes to you, it just feels natural."
    mc "Before I even know it, I'm treating you like that."
    mc "I guess that's what happens when you've been friends for so long."
    hide s_cg2_exp3
    s "Really...?"
    s "Maybe you're right..."
    s "[player]..."
    s "I'm so glad that nothing's changed between us."
    s "Do you think it'll be like this forever?"
    mc "Forever...?"
    "If I'm honest to myself..."
    "There's no telling where we'll each end up for college, or after that."
    "So it wouldn't be fair for me to make any promises."
    "But..."
    mc "Well, I hope so."
    mc "It's been this long already, right?"
    mc "I can't imagine you ever changing, so my hopes are up."
    s "I'm so happy..."
    "Sayori has a whimsical expression in her eyes."
    "We remain silent for a moment."
    "She's so silly and clumsy on the outside that when I see her deep in thought like this..."
    "It makes me not want to disturb her."
    s "I guess we should go back..."
    s "I don't want to worry Monika, you know?"
    mc "Good luck with that."
    mc "She's gonna see your forehead either way."
    s "Not if I hide it under my bangs~"
    play music t8 fadeout 1.0
    scene bg class_day
    show sayori 1a at i11 zorder 2
    with dissolve_cg
    "Sayori hops to her feet."
    show sayori 4p at s11
    s "A-Aaahh--!"
    "She clutches her forehead again."
    mc "Don't stand up so fast after hurting yourself!"
    s "Uuuu..."
    mc "Well, I guess it's too late now..."
    mc "Anyway, let's go."
    scene bg corridor
    with wipeleft_scene
    "I follow Sayori out of the classroom."
    "Sayori plays with her bangs to try to hide the bump, but without much success."
    "In a moment, we make it back to the clubroom."
    scene bg club_day
    show sayori 1a at t21 zorder 2
    show monika 1b at t22 zorder 2
    with wipeleft_scene
    show monika at f22 zorder 3
    m "Ah, you're back!"
    m "Good timing, I was just about ready to start with sharing our poems."
    m 1d "Eh? Sayori, your forehead..."
    show monika at t22 zorder 2
    mc "She's fine, don't worry about--"
    show sayori 4r at f21 zorder 3
    s "I was playing with the crayons and smacked my forehead into the shelf!"
    show sayori at t21 zorder 2
    mc "..."
    show monika 3m at f22 zorder 3
    m "..."
    m 3l "...Well, anyway!"
    m 1a "Were you able to find everything we needed?"
    show monika at t22 zorder 2
    show sayori 1x at f21 zorder 3
    s "Uh-huh! I have it right--"
    s 1n "...Eh?"
    "Sayori frantically glances around herself."
    show sayori 4m at hf21 zorder 3
    s "I...forgot all of the stuff!!"
    show sayori at t21 zorder 2
    mc "Calm down, Sayori."
    mc "I have it all right here."
    mc "I found the poster paper, too."
    show sayori 4b
    show monika 5a at f22 zorder 3
    m "Ahaha!"
    m "Sounds like you ended up doing all the work, [player]."
    show monika at t22 zorder 2
    mc "Ah, well, Sayori--"
    "I fail to come up with an excuse for Sayori."
    show sayori 1q at f21 zorder 3
    s "I made it an adventure!"
    show sayori 1a at t21 zorder 2
    mc "...Yeah, that."
    show monika 1j at f22 zorder 3
    m "Ahaha, okay, okay."
    m 1a "In any case, good work!"
    m "I'll start working on the posters tonight."
    show monika at t22 zorder 2
    show sayori 4x at f21 zorder 3
    s "Me too!"
    show monika at t11 zorder 2
    show sayori at thide behind monika
    hide sayori
    m 4b "...Okay, everyone!"
    m "Are you ready to share your poems?"
    mc "Guess I should grab mine..."
    "After making sure the crayon box is closed tightly, I return to my seat."
    return
